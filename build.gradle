buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

ext.versions = [groovy: '2.4.6',
                logback: '1.1.7',
                slf4j: '1.7.21',
                spock: '1.0-groovy-2.4',
]

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'com.bmuschko.nexus'

    group = 'fi.linuxbox.upcloud'
    version = '0.0.2'

    archivesBaseName = "${rootProject.name}-${project.name}"

    // Java 7
    sourceCompatibility = 1.7

    repositories {
        jcenter()
    }

    dependencies {
        // Everything is written in Groovy
        compile "org.codehaus.groovy:groovy:${versions.groovy}"

        // Dependency injections are marked with standard API
        compile 'javax.inject:javax.inject:1'

        // Libraries do not ship with logging API
        compileOnly "org.slf4j:slf4j-api:${versions.slf4j}"

        // Tests need LogbackConfigurator
        testCompile "ch.qos.logback:logback-classic:${versions.logback}"

        // Tests are written in Spock
        testCompile "org.spockframework:spock-core:${versions.spock}", {
            exclude module: 'groovy-all'
        }
    }

    groovydoc {
        // Java SE 8 APIs
        link("https://docs.oracle.com/javase/8/docs/api/", "java.io", "java.lang", "java.util")
        // Java EE 7 APIs
        link("https://docs.oracle.com/javaee/7/api/", "javax.inject")
        // Groovy APIs
        link("http://docs.groovy-lang.org/docs/groovy-2.4.6/html/gapi/", "groovy.lang")

        [ahc      : 'http.ahc',
         api      : 'api',
         app      : 'app',
         builder  : 'builder',
         core     : 'core',
         jackson  : 'json.jackson',
         resource : 'resource',
         script   : 'script'].each {
            link("http://upcloud.linuxbox.fi/api/${project.version}/${it.key}", "fi.linuxbox.upcloud.${it.value}")
        }
    }

    modifyPom {
        project {
            url 'https://github.com/vmj/groovy-upcloud'

            licenses {
                license {
                    name 'GNU General Public License, version 3'
                    url 'http://www.gnu.org/licenses/gpl-3.0.html'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'vmj'
                    name 'Mikko VÃ¤rri'
                    email 'mikko@varri.fi'
                }
            }

            scm {
                url 'https://github.com/vmj/groovy-upcloud'
                connection 'scm:https://github.com/vmj/groovy-upcloud.git'
                developerConnection 'scm:git@github.com:vmj/groovy-upcloud.git'
            }
        }
    }
}
