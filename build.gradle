buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // https://plugins.gradle.org/plugin/net.saliman.cobertura
        classpath 'net.saliman:gradle-cobertura-plugin:2.5.4'
        // https://github.com/bmuschko/gradle-nexus-plugin
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        // https://github.com/asciidoctor/asciidoctor-gradle-plugin
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2'
        // https://github.com/hierynomus/license-gradle-plugin
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
        // https://github.com/ajoberstar/gradle-git-publish
        classpath 'org.ajoberstar:gradle-git-publish:2.0.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.15.1'
}

// groovydoc depends on subprojects
evaluationDependsOnChildren()

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

//
// merge coverage reports of subprojects after check
//
apply plugin: 'net.saliman.cobertura'
repositories {
    jcenter() // required to fetch the cobertura dependencies
}
cobertura {
    coverageEncoding = 'UTF-8'
    subprojects.each { sp ->
        if (sp.name != 'manual') {
            if (coverageMergeDatafiles)
                coverageMergeDatafiles += file("${sp.buildDir.path}/cobertura/cobertura.ser")
            else
                coverageMergeDatafiles = [file("${sp.buildDir.path}/cobertura/cobertura.ser")]
        }
    }
}
check.finalizedBy(project.tasks.cobertura)

//
// generate one set of groovydocs for the whole project
//
apply plugin: 'groovy'
apply from: 'gradle/versions.gradle'

dependencies {
    compileOnly "org.codehaus.groovy:groovy:${versions.groovy}"
}

groovydoc {
    subprojects.each { sp ->
        if (sp.name != 'manual') {
            source += sp.groovydoc.source
            classpath += sp.groovydoc.classpath
            groovyClasspath += sp.groovydoc.groovyClasspath
        }
    }
}
apply from: 'gradle/groovydoc-highlightjs.gradle'
apply from: 'gradle/groovydoc-links.gradle'

//
// github pages
//
// use './gradlew gitPublishPush' and see http://vmj.github.io/groovy-upcloud/api/index.html
//
apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    repoUri = 'git@github.com:vmj/groovy-upcloud.git'
    branch = 'gh-pages'

    repoDir = file("$buildDir/gitPublish")

    contents {
        from(groovydoc) {
            into 'api'
        }
    }

    commitMessage = 'Generated by gradle-git-publish'
}
gitPublishCopy.dependsOn groovydoc
