buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:$gradleCoberturaPluginVersion"
        classpath "com.bmuschko:gradle-nexus-plugin:$gradleNexusPluginVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:$gradleAsciidoctorPluginVersion"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:$gradleLicensePluginVersion"
        classpath "org.ajoberstar:gradle-git-publish:$gradleGitPublishPluginVersion"
    }
}

plugins {
    id 'com.gradle.build-scan' version '2.4.2'
}

// groovydoc depends on subprojects
evaluationDependsOnChildren()

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

//
// merge coverage reports of subprojects after check
//
apply plugin: 'net.saliman.cobertura'
repositories {
    jcenter() // required to fetch the cobertura dependencies
}
cobertura {
    coverageEncoding = 'UTF-8'
    subprojects.each { sp ->
        if (sp.name != 'manual') {
            if (coverageMergeDatafiles)
                coverageMergeDatafiles += file("${sp.buildDir.path}/cobertura/cobertura.ser")
            else
                coverageMergeDatafiles = [file("${sp.buildDir.path}/cobertura/cobertura.ser")]
        }
    }
}
check.finalizedBy(project.tasks.cobertura)

//
// generate one set of groovydocs for the whole project
//
apply plugin: 'groovy'

dependencies {
    compileOnly "org.codehaus.groovy:groovy:$groovyVersion"
}

groovydoc {
    subprojects.each { sp ->
        if (sp.name != 'manual') {
            source += sp.groovydoc.source
            classpath += sp.groovydoc.classpath
            groovyClasspath += sp.groovydoc.groovyClasspath
        }
    }
}
apply from: 'gradle/groovydoc-highlightjs.gradle'
apply from: 'gradle/groovydoc-links.gradle'

//
// github pages
//
// use './gradlew gitPublishPush' and see http://vmj.github.io/groovy-upcloud/api/index.html
//
apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    repoUri = 'git@github.com:vmj/groovy-upcloud.git'
    branch = 'gh-pages'

    repoDir = file("$buildDir/gitPublish")

    contents {
        from(groovydoc) {
            into 'api'
        }
    }

    commitMessage = 'Generated by gradle-git-publish'
}
gitPublishCopy.dependsOn groovydoc
