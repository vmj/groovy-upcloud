buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.5.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.7'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.11'
}

// groovydoc depends on subprojects
evaluationDependsOnChildren()

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

//
// merge coverage reports of subprojects after check
//
apply plugin: 'net.saliman.cobertura'
repositories {
    jcenter() // required to fetch the cobertura dependencies
}
cobertura {
    coverageEncoding = 'UTF-8'
    coverageMergeDatafiles = subprojects.collect {
        // cobertura plugin does not have a constant for this
        file("${it.buildDir.path}/cobertura/cobertura.ser")
    }
}
check.finalizedBy(project.tasks.cobertura)

//
// generate one set of groovydocs for the whole project
//
apply plugin: 'groovy'
apply from: 'gradle/versions.gradle'

dependencies {
    compileOnly "org.codehaus.groovy:groovy:${versions.groovy}:indy"
}

groovydoc {
    subprojects.each { sp ->
        //if (source)
            source += sp.groovydoc.source
        //else
        //    source = sp.groovydoc.source
        //if (classpath)
            classpath += sp.groovydoc.classpath
        //else
        //    classpath = sp.groovydoc.classpath
        //if (groovyClasspath)
            groovyClasspath += sp.groovydoc.groovyClasspath
        //else
        //    groovyClasspath = sp.groovydoc.groovyClasspath
    }
}
apply from: 'gradle/groovydoc-highlightjs.gradle'
apply from: 'gradle/groovydoc-links.gradle'
