apply plugin: 'java-library'
apply from: "${projectDir}/../gradle/groovy-upcloud.gradle"
apply plugin: 'net.saliman.cobertura'
apply plugin: 'com.bmuschko.nexus'

// Configuring the groovy plugin to work with java-library
// https://docs.gradle.org/current/userguide/java_library_plugin.html#sec:java_library_known_issues_compat
configurations {
    apiElements {
        outgoing.variants.getByName('classes').artifact(
                file: compileGroovy.destinationDir,
                type: ArtifactTypeDefinition.JVM_CLASS_DIRECTORY,
                builtBy: compileGroovy
        )
    }
}

dependencies {
    // Dependency injections are marked with standard API
    implementation 'javax.inject:javax.inject:1'

    // Libraries do not ship with logging API
    implementation "org.slf4j:slf4j-api:${versions.slf4j}"
}

// Augment groovydoc with highlightjs
task highlightjs(type: Copy) {
    from "${projectDir}/../gradle/highlightjs/highlight.pack.js"
    into "${buildDir}/docs/groovydoc/highlightjs"
}
task highlightstyle(type: Copy) {
    from "${projectDir}/../gradle/highlightjs/styles/foundation.css"
    into "${buildDir}/docs/groovydoc/highlightjs/styles"
}
groovydoc.dependsOn highlightjs
groovydoc.dependsOn highlightstyle

groovydoc {
    // Java SE 8 APIs
    link("https://docs.oracle.com/javase/8/docs/api/", "java.io", "java.lang", "java.util")
    // Java EE 7 APIs
    link("https://docs.oracle.com/javaee/7/api/", "javax.inject")
    // Groovy APIs
    link("http://docs.groovy-lang.org/docs/groovy-${versions.groovy}/html/gapi/", "groovy.lang")

    ['api', 'app', 'builder', 'core', 'http-ahc', 'http-spi', 'json-gjson', 'json-jackson', 'json-spi', 'resource', 'script'].each {
        //def url = "file:///Users/vmj/src/groovy-upcloud/groovy-upcloud-$it/build/docs/groovydoc/"
        def url = "http://www.javadoc.io/page/${project.group}/groovy-upcloud-${it}/${project.version}/"
        def packageName = "fi.linuxbox.upcloud.${it.replace('-', '.')}"
        link(url, packageName)
    }

    // Add highlightjs to every page footer
    //def url = "file:///Users/vmj/src/groovy-upcloud/${project.name}/build/docs/groovydoc/"
    def url = "http://www.javadoc.io/page/${project.group}/${project.name}/${project.version}/"
    footer = """
<link rel="stylesheet" href="${url}/highlightjs/styles/foundation.css">
<script src="${url}/highlightjs/highlight.pack.js"></script>
<script>hljs.initHighlightingOnLoad();</script>
"""
}

modifyPom {
    project {
        url 'https://github.com/vmj/groovy-upcloud'

        licenses {
            license {
                name 'GNU General Public License, version 3'
                url 'http://www.gnu.org/licenses/gpl-3.0.html'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'vmj'
                name 'Mikko VÃ¤rri'
                email 'mikko@varri.fi'
            }
        }

        scm {
            url 'https://github.com/vmj/groovy-upcloud'
            connection 'scm:https://github.com/vmj/groovy-upcloud.git'
            developerConnection 'scm:git@github.com:vmj/groovy-upcloud.git'
        }
    }
}
